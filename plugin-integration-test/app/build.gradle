/*
 * (c) Copyright 2023 KineticFire. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/*
EXAMPLE USAGE:

./gradlew -PpluginVersion=1.0.0 clean build

Note that the plugin must be built and at its default location and name:
../plugin/plugin/build/libs/cool-stuff-plugin-<version>.jar
*/


buildscript {
    // require -PpluginVersion
    assert project.hasProperty('pluginVersion') :
            "Missing -PpluginVersion. Example: ./gradlew -PpluginVersion=1.0.0 build"

    def pluginVer   = project.property('pluginVersion').toString()

    def libsDirProp = project.findProperty('libsDir')?.toString()
    def libsDirPath = libsDirProp ?: "${project.projectDir}/../../plugin/plugin/build/libs"
    def pluginJar   = "${libsDirPath}/cool-stuff-plugin-${pluginVer}.jar"

    assert project.file(pluginJar).exists() :
            "Missing plugin JAR at: ${project.file(pluginJar).absolutePath}\n" +
                    "Build the plugin first or pass -PlibsDir=..."

    dependencies {
        classpath files(pluginJar)
    }
}

plugins {
    // Apply the groovy plugin to also add support for Groovy (needed for Spock)
    id 'groovy'

    // Apply the application plugin to add support for building a CLI application in Java.
    id 'application'
}

// Apply the plugin under integration test
apply plugin: com.kineticfire.gradle.cool_stuff.CoolStuffPlugin

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    testImplementation "org.apache.groovy:groovy:4.0.22"
    testImplementation "org.spockframework:spock-core:2.3-groovy-4.0"
    testRuntimeOnly  "org.junit.platform:junit-platform-launcher:1.13.4"

    // This dependency is used by the application.
    implementation "com.google.guava:guava:31.1-jre"
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    // Define the main class for the application.
    mainClass = 'com.kineticfire.gradle.cool_stuff.App'
}

group = 'com.kineticfire.gradle.cool_stuff.example.howdy'
version = 'SNAPSHOT'

jar {
    archiveBaseName = 'gradle-cool-stuff-plugin-example-howdy'
    manifest {
        attributes 'Main-Class' : 'com.kineticfire.gradle.cool_stuff.example.howdy.HowdyWorld'
    }
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}
